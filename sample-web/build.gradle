import org.jetbrains.kotlin.gradle.plugin.KotlinCompilation
import org.jetbrains.kotlin.gradle.targets.js.npm.tasks.KotlinNpmInstallTask

plugins {
  alias(libs.plugins.kotlin.multiplatform)
  id("app.cash.sqldelight")
}

sqldelight {
  databases {
    HockeyDb {
      packageName = "com.example.sqldelight.hockey"
      generateAsync = true
      srcDirs "src/commonMain/sqldelight"
    }
  }
}


kotlin {
  js {
    browser()
    binaries.executable()
    useCommonJs()
  }
  wasmJs {
    browser()
    binaries.executable()
    useCommonJs()
  }

  sourceSets {
    commonMain.dependencies {
      implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.6.0"
      implementation "app.cash.sqldelight:primitive-adapters"
      implementation "org.jetbrains.kotlinx:kotlinx-html:0.11.0"

      implementation "app.cash.sqldelight:web-worker-driver"
      implementation devNpm("copy-webpack-plugin", "9.1.0")
      implementation npm("sql.js", libs.versions.sqljs.get())

      def sqljsWorker = file("${gradle.includedBuild('sqldelight').projectDir}/drivers/web-worker-driver/sqljs")
      implementation npm("@cashapp/sqldelight-sqljs-worker", sqljsWorker)
    }

    commonTest.dependencies {
      implementation 'org.jetbrains.kotlin:kotlin-test'
      implementation libs.kotlin.coroutines.test
    }
  }
}

tasks.withType(KotlinCompilation.class).configureEach {
  compilerOptions {
    freeCompilerArgs.add("-Xexpect-actual-classes")
  }
}

// Workaround yarn concurrency issue - https://youtrack.jetbrains.com/issue/KT-43320
tasks.withType(KotlinNpmInstallTask.class).configureEach {
  args.addAll(["--mutex", "file:${file("../build/.yarn-mutex")}".toString()])
}
